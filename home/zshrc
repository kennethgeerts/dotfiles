### --- ZSH Configuration ---

# Add plugin paths
fpath=(
  $HOME/.zsh/zsh-completions/src
  $fpath
)

# Load completion system with caching
autoload -Uz compinit
compinit -C

# Reasonable shell options
setopt correct          # Auto-correct minor typos
setopt extendedglob     # Enhanced globbing
setopt histignoredups   # Ignore duplicate history entries
setopt sharehistory     # Share command history across sessions
setopt incappendhistory # Immediately append to history file
setopt autopushd        # Push old dir onto stack when changing dirs
setopt pushdignoredups
setopt pushdsilent
setopt interactivecomments

# Completion styles
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' rehash true

### --- Plugins ---

# 1️⃣ History substring search (↑ / ↓ for prefix matches)
source ~/.zsh/zsh-history-substring-search/zsh-history-substring-search.zsh
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# 2️⃣ Autosuggestions
source ~/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
bindkey '^ ' autosuggest-accept  # Ctrl+Space accepts suggestion

# 3️⃣ You Should Use (alias reminder)
source ~/.zsh/zsh-you-should-use/you-should-use.plugin.zsh

# 4️⃣ Syntax highlighting (load *after* everything else)
source ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# 5️⃣ Additional completions
autoload -U compinit && compinit -C

# 6️⃣ FZF fuzzy history search (Ctrl+R)
source ~/.zsh/zsh-fzf-history-search/zsh-fzf-history-search.plugin.zsh
export ZSH_FZF_HISTORY_SEARCH_FZF_ARGS="--height 40% --reverse --border"

### --- Environment Variables & Paths ---

VISUAL=nvim
EDITOR=nvim

path=(
  /Applications/Postgres.app/Contents/Versions/latest/bin
  $HOME/.local/bin
  $path
)

### --- Utility Functions ---

ip() {
  curl -s -4 https://ifconfig.me
}

function ts() {
  date +"%Y%m%d%H%M%S"
}

function flatten() {
  find . -mindepth 2 -type f -exec mv -nv {} . \;
  find . -depth -type d -empty -exec rmdir {} \;
}

function todo() {
  local file="$HOME/Dropbox/todo.md"
  nvim "$file"
}

function wl() {
  local logdir="$HOME/Dropbox/worklog"
  local arg="${1:-0}"

  if [[ "$arg" == "list" || "$arg" == "l" ]]; then
    glow $logdir
    return
  fi

  if date -v +1d >/dev/null 2>&1; then
    local target_date=$(date -v "${arg}d" +%F)
  else
    local target_date=$(date -d "${arg} day" +%F)
  fi

  local logfile="$logdir/$target_date.md"

  if [[ ! -f "$logfile" ]]; then
    echo -e "# $target_date\n\n" > "$logfile"
  fi

  $EDITOR + "$logfile"
}

function rip() {
  echo OUTPUTFORMAT=\'${1:-CD}'/${TRACKNUM}.${TRACKFILE}'\' >! ~/.abcde.conf
  abcde -N -n -x -o mp3
  drutil eject
}

### --- Aliases ---

alias ..="cd .."
alias ...="cd ../.."
alias ....="cd ../../.."
alias c="cursor $PWD"
alias ca="cursor-agent"
alias cat="bat"
alias difff="kitty +kitten diff"
alias ds="kamal deploy -d staging"
alias dp="kamal deploy -d production"
alias find="fd"
alias grep="rg"
alias g="git"
alias ls='eza -lh --group-directories-first --icons=auto'
alias lsa='ls -a'
alias lt='eza --tree --level=2 --long --icons --git'
alias lta='lt -a'
alias lg="lazygit"
alias log="tail -f log/development.log"
alias n="nvim"
alias ping="prettyping"
alias r="rails"
alias top="btop"
alias yayf="yay -Slq | fzf --multi --preview 'yay -Sii {1}' --preview-window=down:75% | xargs -ro yay -S"

### --- Tools ---

# fzf
source <(fzf --zsh)
export FZF_DEFAULT_OPTS=" \
--color=bg+:#414559,bg:#303446,spinner:#f2d5cf,hl:#e78284 \
--color=fg:#c6d0f5,header:#e78284,info:#ca9ee6,pointer:#f2d5cf \
--color=marker:#f2d5cf,fg+:#c6d0f5,prompt:#ca9ee6,hl+:#e78284"

# zoxide
eval "$(zoxide init zsh --cmd j)"

# Java
if (( $+commands[brew] )); then
  export JAVA_HOME="$(brew --prefix openjdk)"
  export PATH="$(brew --prefix openjdk)/bin:$PATH"
fi

# Mise
eval "$(mise activate zsh --shims)"

# OrbStack
source ~/.orbstack/shell/init.zsh 2>/dev/null || :

# Oh My Posh
if [ "$TERM_PROGRAM" != "Apple_Terminal" ]; then
  eval "$(oh-my-posh init zsh --config ~/.prompt.omp.json)"
fi

# pnpm
export PNPM_HOME="$HOME/.pnpm"
case ":$PATH:" in
  *":$PNPM_HOME:"*) ;;
  *) export PATH="$PNPM_HOME:$PATH" ;;
esac

PNPM_COMPLETION=/usr/share/zsh/plugins/pnpm-shell-completion/pnpm-shell-completion.zsh
[[ -f $PNPM_COMPLETION ]] && source $PNPM_COMPLETION

# nnn
BLK="03" CHR="03" DIR="04" EXE="02" REG="07" HARDLINK="05" SYMLINK="05" MISSING="08" ORPHAN="01" FIFO="06" SOCK="03" UNKNOWN="01"
export NNN_COLORS="#04020301;4231"
export NNN_FCOLORS="$BLK$CHR$DIR$EXE$REG$HARDLINK$SYMLINK$MISSING$ORPHAN$FIFO$SOCK$UNKNOWN"

### --- Local overrides ---
[ -f ~/.zshrc.local ] && source ~/.zshrc.local
